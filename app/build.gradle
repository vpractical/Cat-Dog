apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    defaultConfig {
        applicationId rootProject.ext.android.applicationId
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName
        multiDexEnabled true
        multiDexKeepProguard file("multiDexKeep.pro") //分包时，将部分包放入主dex
        ndk {
            // 设置支持的SO库架构
            abiFilters 'armeabi', 'x86', 'armeabi-v7a'//,'x86_64', 'arm64-v8a'
        }
    }

    signingConfigs {
        //开发者签名
        production {
            storeFile file("jks/ywb.jks")
            storePassword "admin@123"
            keyAlias "catdog"
            keyPassword "admin@123"
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            res.srcDirs = ['res']
            aidl.srcDirs = ['src']
            assets.srcDirs = ['assets']
            jni.srcDirs = ['jni']
            jniLibs.srcDirs = ['libs']
            renderscript.srcDirs = ['src']
        }
    }

    buildTypes {
        debug {
            versionNameSuffix "-debug"
            debuggable true
            signingConfig signingConfigs.production
            buildConfigField("String", "API_URL", rootProject.ext.baseUrl.debugUrl)
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.production
            buildConfigField("String", "API_URL", rootProject.ext.baseUrl.releaseUrl)
            // 进行压缩优化
            zipAlignEnabled true
            // 进行资源优化，移除无用的resource文件  跟快速编译不兼容
            // shrinkResources true
            //修改apk包名
            applicationVariants.all { variant ->
                variant.outputs.all { output ->
                    outputFileName = "app-${variant.name}-${defaultConfig.versionName}.apk"
                }
            }
        }
    }
    //移除lint检测的error
    lintOptions {
        abortOnError false
    }

    dexOptions {
        jumboMode true
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    implementation rootProject.ext.dependencies.support_v4
    implementation rootProject.ext.dependencies.design
    implementation rootProject.ext.dependencies.cardview_v7
    implementation rootProject.ext.dependencies.appcompat_v7
    implementation project(':mvp_library')
    implementation project(':imageloader')
    implementation project(':vitamio')
    implementation 'com.android.support:multidex:1.0.3'
    annotationProcessor "com.google.dagger:dagger-compiler:2.15"
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
//    annotationProcessor 'com.github.bumptech.library说明:compiler:4.7.1'//glide高级功能

    //首页顶部的tab
    implementation 'com.ashokvarma.android:bottom-navigation-bar:1.4.1'
    //一个操作view的库
    implementation 'com.nineoldandroids:library:2.4.0'
    //约束布局
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    //扇形菜单库
    implementation 'com.oguzdev:CircularFloatingActionMenu:1.0.2'
    //人脸识别依赖的库,SurfaceView相关
    implementation 'com.guo.android_extend:android-extend:1.0.5'
}
